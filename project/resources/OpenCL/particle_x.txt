static void				particle_reset_target(
						constant vector3 *global_color,
						global char *is_alive,
						global vector3 *position,
						global vector3 *velocity,
						global vector3 *acceleration,
						global vector4 *color,
						global int *born_by_emitter,
						int id)
{
	is_alive[id] = 0;
	born_by_emitter[id] = -1;
	position[id] = vector3_empty();
	velocity[id] = vector3_empty();
	acceleration[id] = vector3_empty();
	color[id] = (vector4){global_color->x, global_color->y, global_color->z, 0};
}

kernel void				particle_reset(
						constant vector3 *global_color,
						global char *is_alive,
						global vector3 *position,
						global vector3 *velocity,
						global vector3 *acceleration,
						global vector4 *color,
						global int *born_by_emitter)
{
	particle_reset_target(
		global_color,
		is_alive,
		position,
		velocity,
		acceleration,
		color,
		born_by_emitter,
		get_global_id(0));
}

static float			distribute_float(float start, float finish, float ratio)
{
	return (start + (finish - start) * ratio);
}

kernel void				particle_update(
						constant vector3 *global_color,
						global char *is_alive,
						global vector3 *position,
						global vector3 *velocity,
						global vector3 *acceleration,
						global vector4 *color)
{
	const int			global_id = get_global_id(0);

	const vector3		change = vector3_add(velocity[global_id], acceleration[global_id]);

	position[global_id] = vector3_add(position[global_id], change);
	velocity[global_id] = vector3_add(vector3_multiply(velocity[global_id], 0.9999), acceleration[global_id]);
	acceleration[global_id] = vector3_multiply(acceleration[global_id], 0.f);

	if (is_alive[global_id])
	{
		const vector3		finish_color = {1, 1, 1};

		const float			length = vector3_length(velocity[global_id]);
		const float			ratio = CLAMP(length / 10, 0, 1);

		color[global_id].x = distribute_float(global_color->x, finish_color.x, ratio);
		color[global_id].y = distribute_float(global_color->y, finish_color.y, ratio);
		color[global_id].z = distribute_float(global_color->z, finish_color.z, ratio);
		color[global_id].w = 1;
	}
	else
		color[global_id] = vector4_empty();
}